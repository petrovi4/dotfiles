# Инфа про разные файлы zsh
# https://unix.stackexchange.com/questions/71253/what-should-shouldnt-go-in-zshenv-zshrc-zlogin-zprofile-zlogout

set -g default-command /bin/zsh
set -g default-shell /bin/zsh


# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi


# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)


# echo 'Load paths'

# загружаем все дополнения PATH переменной
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done


# echo 'Load all, except paths and completions'

# грузим все zsh файлы кроме path (загрузили выше) и completion (загрузим после автокомплита)
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done


# echo 'Load autocomplete'

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit


# echo 'Load topics completion'

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
# Search through history for previous commands matching everything up to current cursor position. Move the cursor to the end of line after each match.
# autoload -U up-line-or-beginning-search
# autoload -U down-line-or-beginning-search
# zle -N up-line-or-beginning-search
# zle -N down-line-or-beginning-search
# bindkey "^[[A" up-line-or-beginning-search # Up
# bindkey "^[[B" down-line-or-beginning-search # Down

